;; -*- mode: clojure;-*-

(ns torque.lang.list
  (:require
    [torque.lang.protocols :refer :all]
    [torque.lang.boot :refer :all]
    [torque.lang.common.seq :as seq]))

(deftype List [meta first rest count]

  ;; tag protocol to hide the fact that there
  ;; are different types that act as lists
  AList

  IPrintable
  (-str [this]
    (seq/pr-str "(" ")" this))

  ICloneable
  (-clone [_]
    (new List meta first rest count))

  IWithMeta
  (-with-meta [o meta]
    (new List meta first rest count))

  IMeta
  (-meta [o] meta)

  ISeq
  (-first [_]
    first)
  (-rest  [_]
    (if (= count 1)
      ()
      rest))

  INext
  (-next [_]
    (if (= count 1)
      nil
      rest))

  IStack
  (-peek [_]
    first)
  (-pop [coll]
    (-rest coll))

  ICollection
  (-conj [coll o]
    (new List meta o coll (inc count)))

  IEquiv
  (-equiv [o other]
    (seq/equiv o other))

  ISeqable
  (-seq [o]
    (if (== count 0)
      nil
      o))

  ICounted
  (-count [_] count)

  IReduce
  (-reduce [coll f]
    (seq/reduce f coll))
  (-reduce [coll f init]
    (seq/reduce f init coll)))

(def empty-list
  (fn* []
    (new List nil nil nil 0)))

(def empty (empty-list))

(def list
  (fn* list [& xs]
    (if (nil? xs)
      empty
      (loop* [out empty
              n   (dec (alength xs))]
        (if (<= 0 n)
          (recur (-conj out (aget xs n))
                 (dec n))
          out)))))
